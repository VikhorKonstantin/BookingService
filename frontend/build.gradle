buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.3.1'
    }
}

apply plugin: 'base'
apply plugin: 'com.moowork.node'

def buildFolder = 'build'
npm_run_build {
    // make sure the build task is executed only when appropriate files change
    inputs.files fileTree('public')
    inputs.files fileTree('src')
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    outputs.dir buildFolder
}
configurations {
    npmResources
}
configurations.default.extendsFrom(configurations.npmResources)
artifacts {
    npmResources(file(buildFolder)) {
        builtBy npm_run_build
        type 'folder'
    }
}
assemble.dependsOn npm_run_build
clean {
    delete 'build'
}